-- from AADL v0.99 pp.146 -- 154

package gps
public
	data signal_data
	end signal_data;

	data position
	end position;

	data implementation position.radial
	end position.radial;

	data implementation position.cartesian
	end position.cartesian;
end gps;

process foo
features	
	Initcmd : in event port;
	Signal : in data port gps::signal_data;
	Result1 : out data port gps::position.radial;
	Result2 : out data port gps::position.cartesian;
	Status : out event port;
flows
	--  Two flows split from the same input
	Flow1 : flow path signal -> result1;
	Flow2 : flow path signal -> result2;
	--  An input is consumed by process foo through its initcmd port
	Flow3 : flow sink initcmd;
	--  An output is generated (produced) by process foo and made
	--  available through its port Status
	Flow4 : flow source Status;
end foo;

thread bar
features
	p1 : in data port gps::signal_data;
	p2 : out data port gps::position.radial;
	p3 : out event port;
flows
	fs1 : flow path p1 -> p2;
	fs2 : flow source p3;
end bar;

thread implementation bar.basic
end bar.basic;

thread baz
features
	p1 : in data port gps::position.radial;
	p2 : out data port gps::position.cartesian;
	p3 : out data port gps::position.radial;
	reset : in event port;
flows
	fs1 : flow path p1 -> p2;
        fs3 : flow path p1 -> p3;
	fsink : flow sink reset;
end baz;

thread implementation baz.basic
end baz.basic;

process implementation foo.basic
subcomponents	
	A : thread bar.basic;
	--  bar has a flow path fs1 from port p1 to p2
	--  bar has a flow source fs2 to p3
	B : thread baz.basic;
	C : thread baz.basic;
	--  baz has a flow path fs1 from port p1 to p2
	--  baz has a flow path fs2 from port p1 to p3
	--  baz has a flow sink fsink in port reset
connections
	conn1 : data port signal -> A.p1;
	conn2 : data port A.p2 -> B.p1;
	conn3 : data port B.p3 -> result1;
	conn4 : data port A.p2 -> C.p1;
	conn5 : data port C.p2 -> result2;
	conn6 : event port A.p3 -> status;
	connToThread : event port initcmd -> C.reset;
flows
	Flow1 : flow path
		signal -> conn1 -> A.fs1 -> conn2 -> B.fs1 -> conn3 -> result1;
	Flow2 : flow path
		signal -> conn1 -> A.fs1 -> conn4 -> C.fs3 -> conn5 -> result2;
	Flow3 : flow sink initcmd -> connToThread -> C.fsink;
	--  a flow source may start in a subcomponent,
	--  i.e;, the first named element us a flow source
	Flow4 : flow source A.fs2 -> conn6 -> status;
end foo.basic;
