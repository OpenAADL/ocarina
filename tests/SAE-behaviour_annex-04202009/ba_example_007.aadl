--  Behaviour Annex 2.9 example (7)
--  Annex X.8 Behavior Expression Language

package Test
public
  with Base_Types;

  thread merger
   features
    p1 : in event data port Base_types::integer;
    p2 : in event data port Base_types::integer;
    m : out event data port Base_types::integer;
  end merger;

  thread implementation merger.twopersistentstates
   subcomponents
    x1 : data Base_types::integer;
    x2 : data Base_types::integer;

   annex behavior_specification {**
    states
     s0 : initial complete state;
     comp : state;
     next1, next2 : complete state;
    transitions
     s0 -[ on dispatch p1 ]-> next2 { x1 := p1 };
     s0 -[ on dispatch p2 ]-> next1 { x2 := p2 };
     next1 -[ on dispatch p1 ]-> comp { x1 := p1 };
     next2 -[ on dispatch p2 ]-> comp { x2 := p2 };
     comp -[ x1 < x2 ]-> next1 { m!(x1) };
     comp -[ x2 <= x1 ]-> next2 { m!(x2) };
   **};
  end merger.twopersistentstates;

  thread implementation merger.portbased
   annex behavior_specification {**
    states
     s0 : initial complete state;
     comp : state;
     next1 : complete state;
     next2 : complete state;
    transitions
     s0 -[ on dispatch p1 ]-> next2;
     s0 -[ on dispatch p2 ]-> next1;
     next1 -[ p1 < p2 ]-> next1 { m!(p1) };
     next1 -[ p2 <= p1 ]-> next2 { m!(p2) };
     next2 -[ p1 < p2 ]-> next1 { m!(p1) };
     next2 -[ p2 <= p1 ]-> next2 { m!(p2) };
   **};
  end merger.portbased;

end Test;
