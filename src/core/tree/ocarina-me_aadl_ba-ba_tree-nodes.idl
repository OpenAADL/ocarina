/*****************************************************************************
**                                                                          **
**                           OCARINA COMPONENTS                             **
**                                                                          **
**                  OCARINA.ME_BEHAVIOR.BEHAVIOR_TREE.NODES                 **
**                                                                          **
**               Copyright (C) 2004-2009, GET-Telecom Paris.                **
**                                                                          **
** Ocarina  is free software;  you  can  redistribute  it and/or  modify    **
** it under terms of the GNU General Public License as published by the     **
** Free Software Foundation; either version 2, or (at your option) any      **
** later version. Ocarina is distributed  in  the  hope  that it will be    **
** useful, but WITHOUT ANY WARRANTY;  without even the implied warranty of  **
** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General **
** Public License for more details. You should have received  a copy of the **
** GNU General Public License distributed with Ocarina; see file COPYING.   **
** If not, write to the Free Software Foundation, 51 Franklin Street, Fifth **
** Floor, Boston, MA 02111-1301, USA.                                       **
**                                                                          **
** As a special exception,  if other files  instantiate  generics from this **
** unit, or you link  this unit with other files  to produce an executable, **
** this  unit  does not  by itself cause  the resulting  executable to be   **
** covered  by the  GNU  General  Public  License. This exception does not  **
** however invalidate  any other reasons why the executable file might be   **
** covered by the GNU Public License.                                       **
**                                                                          **
**                 Ocarina is maintained by the Ocarina team                **
**                       (ocarina-users@listes.enst.fr)                     **
**                                                                          **
*****************************************************************************/

module Ocarina::ME_AADL_BA::BA_Tree::Nodes {

  /******************/
  /* Internal types */
  /******************/

  typedef long  Name_Id;
  typedef long  Value_Id;

  /******************/
  /* Internal nodes */
  /******************/

  //  Node_Id:
  //    Node type from which all the other node types derive.
  //
  //  * Next_Node:
  //    Any node can be part of a container. Thus, the container
  //    includes a list of nodes. Next_Node designates the next node
  //    in the list of the container. It is forbidden to define
  //    another attribute to point to a "next" node.

  interface Node_Id {
    Node_Id  Next_Node;
  };

  //  List_Id:
  //    List type used to represent the entities contained in a
  //    container.
  //
  //  * First_Node, Last_Node:
  //    First and last nodes of a unidirectional list.

  interface List_Id {
    Node_Id  First_Node;
    Node_Id  Last_Node;
  };

  interface Node_Container : Node_Id {
    Node_Id  Item;
    Node_Id  Extra_Item;
  };

  interface Behavior_Entity : Node_Id {
    Node_Id  BE_Container;   //  Behavior entity container, hierarchic parent
    Node_Id  Next_Entity;    // Used to build list of elements
  };

  interface Named_Behavior_Entity : Behavior_Entity {
    Node_Id  Identifier;
  };

  interface Identifier : Behavior_Entity {
    Name_Id  Name;
    Name_Id  Display_Name;
    Node_Id  Corresponding_Entity;
    Node_Id  Scope_Entity;           // not use
    Node_Id  Homonym;                // not use
  };

  interface Identifier_With_Value : Identifier {
    boolean  Is_Others;
    Node_Id  Value_Constant;
  };

/**********************************************************************
 ** These following interfaces file describe the Behavior tree.      **
 **********************************************************************/

/*******************************
 ** 3. Behavior specification **
 *******************************/

  //  behavior_annex ::=
  //    [ variables { behavior_variable }+ ]
  //    [ states { behavior_state }+ ]
  //    [ transitions { behavior_transition }+ ]

  interface Behavior_Annex : Behavior_Entity {
    List_Id  Variables;
    List_Id  States;
    List_Id  Transitions;
  };

  //  behavior_variable ::=
  //    local_variable_identifier { , local_variable_identifier }*
  //    : data_unique_component_classifier_reference ] 
  //    [ { { data_classifier_property_association }+ } ] ;

  //  unique_component_classifier_reference ::= 
  //  { package_identifier :: }* component_type_identifier
  //  [ . component_implementation_identifier ]

  //  data_classifier_property_association : la syntaxe n'est pas claire

  interface Component_Classifier_Ref : Behavior_Entity {
    List_Id  Package_Name;
    Node_Id  Component_Type;
    Node_Id  Component_Impl;

    Node_Id  Full_Identifier;  // ex: package::comp_type.comp_impl
    Node_Id  Corresponding_Declaration;  // AADL Declarative tree node
  };

  interface Behavior_Variable : Behavior_Entity {
    List_Id  Identifiers;      // list of variable_identifier
    Node_Id  Classifier_Ref;   // data unique component classifier reference
  };


  //  behavior_state ::=
  //    behavior_state_identifier { , behavior_state_identifier }*
  //        : behavior_state_kind state;

  //  behavior_state_kind ::=
  //    [ initial ] [ complete ] [ final ]

  interface Behavior_State : Behavior_Entity {
    List_Id  Identifiers;
    octet    State_Kind;
  };


  //  behavior_transition ::=
  //    execution_behavior_transition | mode_transition

  interface Behavior_Transition : Behavior_Entity {
    Node_Id Transition;
  };

  //  execution_behavior_transition ::=
  //    [ transition_identifier [ [ behavior_transition_priority ] : ]
  //        source_state_identifier { , source_state_identifier }*
  //           -[ [ behavior_condition ] ]->
  //        destination_state_identifier [ behavior_action_block ] ;

  //  behavior_transition_priority ::= numeral
  
  interface Execution_Behavior_Transition : Behavior_Entity {
    List_Id  Sources;
    Node_Id  Destination;  // fixme only one destination ?
    Node_Id  Behavior_Condition;
    Node_Id  Behavior_Action_Block;

    Node_Id  Behavior_Transition_Idt;
    Node_Id  Behavior_Transition_Priority; 
  };


  //  behavior_condition ::=
  //    execute_condition | dispatch_condition | mode_condition

  interface Behavior_Condition : Behavior_Entity {
    Node_Id  Condition;
  };


  //  execute_condition ::= [ logical_value_expression |  behavior_action_block_timeout_catch | otherwise ]
  //  behavior_action_block_timeout_catch : la syntaxe n'est pas claire
  
    interface Execute_Condition : Behavior_Entity {
      Node_Id  Value_Expression;
      boolean  Is_Otherwise;
    };

  // mode_condition ::= 
  //  on trigger_logical_expression

    interface Mode_Condition : Behavior_Entity {
      Node_Id  Trigger_Logical_Expr;
    };

  // trigger_logical_expression ::=
  //  event_trigger { logical_operator event_trigger }*

    interface Trigger_Logical_Expression : Behavior_Entity {
      List_Id  Event_Triggers;
    };

  // event_trigger ::=
  //  in_event_port_component_reference
  // | in_event_data_port_component_reference
  // | ( trigger_logical_expression )

    interface Event_Trigger : Behavior_Entity {
      Node_Id  Port_Component_Ref;
      Node_Id  Trigger_Log_Expr;
    };

  // port_component_reference ::=
  //  subcomponent_name.port_identifier

    interface Port_Component_Reference : Behavior_Entity {
      Node_Id  Subcomponent_Name;
      Node_Id  Port_Idt;
    }; 
  
  //  mode_transition ::=
  //    mode_transition_identifier :
  //        source_mode_identifier 
  //          –[ mode_transition_condition ]->
  //        destination_mode_identifier [ { behavior_actions } ] ;

  interface  Mode_Transition : Behavior_Entity {
    Node_Id  Source;
    Node_Id  Destination;
    Node_Id  Mode_Transition_Condition;
    List_Id  Behavior_Actions;
  };
  //  Behavior_Entity has a field identifier, here it represents 
  //  mode_transition_identifier



/***********************************************
 ** 4. Thread Dispatch Behavior Specification **
 ***********************************************/


  // dispatch_condition ::= 
  //  on dispatch [ dispatch_trigger_condition ] [ frozen frozen_ports ]

  interface Dispatch_Condition_Thread : Behavior_Entity {
    Node_Id  Dispatch_Trigger_Condition;
    List_Id  Frozen_Ports;
  };

  // dispatch_trigger_condition ::=
  //    dispatch_trigger_logical_expression
  //  | provides_subprogram_access_name
  //  | stop
  //  | completion_relative_timeout_condition_and_catch
  //  | dispatch_relative_timeout_catch

  //  completion_relative_timeout_condition_and_catch ::=
  //    timeout [ (event_port_name*) ] behavior_time

  //  dispatch_relative_timeout_catch ::=
  //    timeout

  //  frozen_ports ::=
  //    in_port_name { , in_port_name }*

  interface Dispatch_Trigger_Condition : Behavior_Entity {
    List_Id  Dispatch_Conjunction;
    Node_Id  Behavior_Time;
    octet    Trigger_Kind;
  };      

  //  dispatch_trigger_logical_expression ::=
  //    dispatch_conjunction { or dispatch_conjunction }*
  
  //  dispatch_conjunction ::=
  //    dispatch_trigger { and dispatch_trigger }*
 
  interface Dispatch_Conjunction : Behavior_Entity {
    List_Id  Dispatch_Triggers;
  }; 
  
  //  dispatch_trigger ::=
  //    in_event_port_identifier
  //  | in_event_data_port_identifier


/***********************************************
 ** 5. Mode Transition Behavior Specification **
 ***********************************************/

  //  mode_transition_condition ::=
  //    on mode transition mode_transition_logical_expression


  //  mode_transition_logical_expression ::=
  //    mode_transition { or mode_transition }*

  //  mode_transition_conjunction ::=
  //    ( mode_transition_trigger { and mode_transition_trigger_event } * )


  //  mode_transition_trigger ::= <defined in core AADL>



/*********************************
 ** 7. Behavior Action Language **
 *********************************/

  interface Logical_Expression : Behavior_Entity { };

  // behavior_action_block ::= { behavior_actions } [ timeout behavior_time ]

  interface Behavior_Action_Block : Behavior_Entity {
    Node_Id  Behav_Acts;
    Node_Id  Behavior_Time;
  };

  // behavior_actions ::=
  //   behavior_action
  //   | behavior_action_sequence
  //   | behavior_action_set

  // behavior_action_sequence ::=
  //   behavior_action { ; behavior_action }+

  // behavior_action_set ::=
  //   behavior_action { & behavior_action }+


  interface Behavior_Actions : Behavior_Entity {
    Node_Id  Behavior_Action;
    List_Id  Behavior_Action_Sequence;
    List_Id  Behavior_Action_Set;
    List_Id  Scope_BA_Entities;  // this attribute is used to enable analysis
			       // of the element in For/Forall construct
			       // that acts as local variable with the scope
			       // of the For/Forall construct
 };

 interface BA_Entity_List : List_Id {};

  //  behavior_action ::= 
  //    basic_action
  //  | if ( logical_value_expression ) behavior_actions
  //    { elsif ( logical_value_expression ) behavior_actions }*
  //    [ else behavior_actions ]   
  //    end if 
  //  | for ( element_identifier : data_unique_component_classifier_reference
  //  in element_values ) { behavior_actions }
  //  | forall ( element_identifier : data_unique_component_classifier_reference
  //  in element_values ) { behavior_actions }
  //  | while ( logical_value_expression ) { behavior_actions }
  //  | do behavior_actions until ( logical_value_expression )

  interface Behavior_Action : Behavior_Entity {
    Node_Id  Action;
  };

  interface Conditional_Statement : Behavior_Entity {
    Node_Id  Logical_Expr;
    Node_Id  Behav_Acts;
  };
  //  fixme : this node is define by us not in the draft


  interface If_Cond_Struct : Behavior_Entity {
    Node_Id  If_Statement;    //  node kind is Conditional_Statement
    List_Id  Elsif_Statement;  //  i.e
    Node_Id  Else_Statement;  //  i.e
  };

  interface For_Cond_Structure : Behavior_Entity {
    Node_Id  Element_Idt;
    Node_Id  Classifier_Ref;
    Node_Id  In_Element_Values;
    Node_Id  Behav_Acts;
  };

  interface While_Cond_Structure : Behavior_Entity {
    Node_Id  Logical_Expr;
    Node_Id  Behav_Acts;
  };

  interface ForAll_Cond_Structure : Behavior_Entity {
    Node_Id  Element_Idt;
    Node_Id  Classifier_Ref;
    Node_Id  In_Element_Values;
    Node_Id  Behav_Acts;
  };

  interface DoUntil_Cond_Structure : Behavior_Entity {
    Node_Id  Logical_Expr;
    Node_Id  Behav_Acts;
  };

  //  element_values ::=
  //    integer_range
  //  | event_data
  //  | array_data_component_reference

  interface Element_Values : Behavior_Entity {
    Node_Id  Entity;  //  kind of entity is K_Identifier or K_Integer_Range
                      //  or K_Data_Component_Reference
  };


  //  basic_action ::=
  //    assignment_action
  //  | communication_action
  //  | timed_action 

  //  assignment_action::=
  //    target := ( value_expression | any )

  //  target ::=
  //    outgoing_port_name  
  //  | internal_port_name
  //  | outgoing_subprogram_parameter_identifier 
  //  | data_component_reference
  //  | outgoing_port_prototype_name
  
  interface Assignment_Action : Behavior_Entity {
    Node_Id  Target;
    Node_Id  Value_Expression;

    boolean  Is_Any;
  };


  //  communication_action ::=
  //    subprogram_prototype_name ! [ ( subprogram_parameter_list ) ]
  //  | required_subprogram_access_name ! [ ( subprogram_parameter_list ) ]
  //  | subprogram_subcomponent_name ! [ ( subprogram_parameter_list ) ]
  //  | output_port_name ! [ ( value_expression ) ]
  //  | internal_port_name ! [ ( value_expression ) ]
  //  | input_port_name >>
  //  | input_port_name ? [ ( target ) ]
  //  | required_data_access_name !<
  //  | required_data_access_name !>
  //  | required_data_access_name . provided_subprogram_access_name !
  //    [ ( subprogram_parameter_list ) ]
  //  | data_subcomponent_name . provided_subprogram_access_name !
  //    [ ( subprogram_parameter_list ) ]
  //  | local_variable_name . provided_subprogram_access_name !
  //    [ ( subprogram_parameter_list ) ]

  //  subprogram_parameter_list ::=
  //    parameter_label { , parameter_label } *
  
  interface Communication_Action : Behavior_Entity {
    Node_Id  Identifier;
    List_Id  Subprogram_Parameter_List;
    Node_Id  Target;
    boolean  Is_Subprogram_Call;
    octet    Comm_Kind; // ! , ? , >> , !< , !>
  };


  //  timed_action ::= 
  //    computation ( behavior_time [ .. behavior_time ] )
  //    [ in binding ( processor_unique_component_classier_reference
  //    { , processor_unique_component_classifier_reference }* ) ]


  interface Timed_Act : Behavior_Entity {
    Node_Id  Fst_Behavior_Time;
    Node_Id  Scd_Behavior_Time;
    boolean  Is_InBinding;
    List_Id  Processor_Idt;
  };


  //  parameter_label ::=
  //    in_parameter_value_expression | out_parameter_target

  interface Parameter_Label : Behavior_Entity {
    Node_Id  Parameter;
    boolean  Is_In;
    boolean  Is_Out;
  };


  //  data_component_reference ::=
  //   data_subcomponent_name { . data_subcomponent_name }*
  // | data_access_feature_name { . data_field }*
  // | local_variable_name { . data_field }* 
  // | data_access_feature_prototype_name { . data_field }*

  //  data_field ::=
  //   data_subcomponent_name
  // | data_access_feature_name
  // | data_access_feature_prototype_name

  interface Data_Component_Reference : Behavior_Entity {
    List_Id  Identifiers;
  };

  //  name ::= 
  //    { identifier . }*
  //    identifier { array_index }*

  //  array_index ::=
  //    [ integer_value ]

  interface Name : Behavior_Entity {
    List_Id  Idt;
    List_Id  Array_Index;
  };



/*************************************
 ** 8. Behavior Expression Language **
 *************************************/

  //  value_variable ::=
  //    incoming_port_name
  //  | incoming_port_name ? 
  //  | incoming_subprogram_parameter_identifier
  //  | incoming_port_prototype_name 
  //  | data_component_reference
  //  | port_name ’ count
  //  | port_name ’ fresh
  //  | port_name ’ updated

  interface Value_Variable : Behavior_Entity {
    Node_Id  Identifier;  // kind K_Identifier or K_Name or K_Data_Component_Reference

    boolean  Is_Count;
    boolean  Is_Fresh;
    boolean  Is_Updated;
    boolean  Is_Interrogative;
  };


  //  value_expression ::=
  //    relation { logical_operator relation}*

  interface Value_Expression : Behavior_Entity {
    List_Id  Relations;
  };

 
  //  relation ::=
  //    simple_expression [relational_operator simple_expression]

  interface Relation : Behavior_Entity {
    List_Id  Simple_Exprs;
  };

  //  simple_expression ::=
  //    [unary_adding_operator] term {binary_adding_operator term}*

  interface Simple_Expression : Behavior_Entity {
    List_Id  Term_And_Operator;
  };
  

  //  term ::= factor {multiplying_operator factor}*

  interface Term : Behavior_Entity {
    List_Id  Factors;
  };


  //  factor ::= value [** value] | abs value | not value

  //  value ::=
  //    value_variable | numeric_literal | string_literal | boolean_literal |
  //    property_constant | property_reference | (value_expression)

  interface Factor : Behavior_Entity {
    boolean  Is_Abs;
    boolean  Is_Not;

    Node_Id  Lower_Value;
    Node_Id  Upper_Value;  //  **
  };


  //  property_constant ::=
  //    [ property_set_identifier :: ] property_constant_identifier

  interface Property_Constant : Named_Behavior_Entity {
    Node_Id  Property_Set;
  };

  //  property_reference ::=
  //  # [ property_set_identifier :: ] property_name 
  //  | component_element_reference # property_name 
  //  | unique_component_classifier_reference # property_name 


  interface Property_Reference : Behavior_Entity {
    Node_Id  Property_Set_Idt;
    Node_Id  Entity;
    Node_Id  Property_Name;
  };

  //  property_name ::=
  //  property_identifier { property_field }*

  interface Property_Name : Behavior_Entity {
    Node_Id  Property_Idt;
    List_Id  Property_Field;
  };

  //  component_element_reference ::=
  //  subcomponent_identifier
  //  | local_variable_identifier
  //  | bound_prototype_identifier
  //  | feature_identifier
  //  | self
 
  interface Component_Element_Reference : Behavior_Entity {
    Node_Id  Ident;
    boolean  Is_Self;
  };

  //  property_field ::=
  //    [ integer_value ] 
  //  | . field_identifier 
  //  | . upper_bound 
  //  | . lower_bound


  interface Property_Field : Behavior_Entity {
    boolean  Is_Upper_Bound;
    boolean  Is_Lower_Bound;

    Node_Id  Entity;
  }; 

  //  -- operators in increasing order of precedence
  //  logical_operator ::= and | or | xor | and then | or else

  //  relational_operator ::=  =  | !=    | <   | <= | > | >=

  //  binary_adding_operator ::=  +    | - 

  //  unary_adding_operator ::=   -

  //  multiplying_operator ::=   *      | /     | mod | rem

  //  highest_precedence_operator ::=       **   | abs | not

  interface Operator : Behavior_Entity {
    octet  Operator_Category;
  };


  //  boolean_literal ::= true | false

  interface Boolean_Literal : Behavior_Entity {
    boolean  Is_True;  //  if not false
  };


  //  integer_range ::= integer_value .. integer_value

  interface Integer_Range : Behavior_Entity {
    Node_Id  Lower_Int_Val;
    Node_Id  Upper_Int_Val;
  };


  // integer_value ::=
  //   integer_value_variable
  // | integer_numerical_literal
  // | integer_property_constant

  interface Integer_Value : Behavior_Entity {
    Node_Id  Entity;
  };


  // behavior_time ::= integer_value unit_identifier

  interface Behavior_Time : Behavior_Entity {
    Node_Id  Integer_Value;
    Node_Id  Unit_Identifier;
  };

  // Add by Telecom ParisTech
  interface Literal : Behavior_Entity {
    Value_Id  Value;
  };
};
