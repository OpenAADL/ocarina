property set Deployment is

  Allowed_Transport_APIs : type enumeration
    (BSD_Sockets,
     SpaceWire);
  -- Supported transport API

  Transport_API : Deployment::Allowed_Transport_APIs applies to (bus);
  --  Transport API of a bus

  Location : aadlstring applies to (processor, device);
  --  Processor IP address (BSD_Sockets specific)

  Port_Number : aadlinteger applies to (process, device);
  --  IP port number of a process (BSD_Sockets specific)

  Process_ID : aadlinteger applies to (process, device);
  --  Process identifier (SpaceWire specific)

  Channel_Address : aadlinteger applies to (process, device);
  --  Communication channel address (SpaceWire specific)

  Protocol_Type : type enumeration (iiop, diop);
  --  Supported communication protocols
  Protocol : Deployment::Protocol_Type applies to (system);

  Allowed_Execution_Platform : type enumeration
   (Native,                   -- Native platforms (GNU/Linux, Solaris, Windows...)
    Native_Compcert,          -- Native platforms using the Compcert compiler
    bench,                    -- Benchmark platform (native with instrumentation).
    LEON_RTEMS,               -- LEON2 board or tsim-leon (RTEMS)
    LEON_RTEMS_POSIX,         -- LEON2 board or tsim-leon (RTEMS)
    LEON3_XTRATUM,            -- LEON3 with Xtratum
    LEON3_XM3,                -- RTEMS for XTRATUM/LEON3
    LEON_ORK,                 -- LEON2 board or tsim-leon (ORK)
    LEON_GNAT,                -- LEON2 board or qemu (GNATPRO/HI-E)
    LINUX32,                  -- Linux 32 bits
    LINUX32_XENOMAI_NATIVE,   -- Linux 32 bits with native Xenomai
    LINUX32_XENOMAI_POSIX,    -- Linux 32 bits with Xenomai and POSIX skin
    LINUX64,                  -- Linux 64 bits
    ERC32_ORK,                -- ERC32 board or tsim-erc32 (ORK)
    ARM_DSLINUX,              -- Nintendo DS (tm) (DSLinux)
    ARM_N770,                 -- Nokia N770 (tm)
    GUMSTIX_RTEMS,            -- Gumstix under RTEMS
    NDS_RTEMS,                -- Nintendo DS under RTEMS
    X86_RTEMS,                -- x86 under RTEMS
    X86_RTEMS_POSIX,          -- x86 under RTEMS with POSIX layer
    X86_LINUXTASTE,           -- TASTE-specific linux distribution
    MARTE_OS,                 -- MaRTE OS
    VXWORKS                   -- VXWORKS
    );
  --  Supported platforms

  Execution_Platform : Deployment::Allowed_Execution_Platform
    applies to (all);
  --  Execution platform of a processor

  Supported_Execution_Platform : list of Deployment::Allowed_Execution_Platform
    applies to (device);
  --  List execution platforms supported by a particular driver

  Runtime : type enumeration
    (PolyORB_HI_C,
     PolyORB_HI_Ada,
     POK);
  --  List of supported runtime

  Supported_Runtime : Deployment::Runtime applies to (all);
  --  List the runtime compatible with the component

  Priority_Type : type aadlinteger 0 .. 255;

  Priority : Deployment::Priority_Type applies to (data, thread);
  --  Thread and data component priority

  Driver_Name : aadlstring applies to (device);

  Configuration : aadlstring applies to (device);

  Config : aadlstring applies to (device);

  ASN1_Module_Name : aadlstring applies to (all);

  Help : aadlstring applies to (all);

  Version : aadlstring applies to (all);

  Configuration_Type : classifier (data) applies to (all);

end Deployment;
