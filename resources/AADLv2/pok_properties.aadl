property set POK is
  Security_Level : aadlinteger applies to 
         (virtual processor, virtual bus, process, bus, event data port, event port, data port);
  -- Means two things :
  --   * security_level that a partition is allowed to access
  --   * security_level provided by a virtual bus : ensure that
  --     the virtual bus can transport data from and/or to partitions
  --     that have this security level.

  Criticality : aadlinteger applies to 
         (virtual processor);
  --  Represent the criticality level of a partition.

  Handler : aadlstring applies to 
         (virtual processor);
  -- Error handler for each partition
  -- By default, the code generator can create a function
  -- which name derives from the partition name. Instead, the
  -- model can provide the name of the handler with this property.

  Topics : list of aadlstring applies to 
         (virtual processor, virtual bus);
  -- Means two things :
  --   * The topics allowed on a specific virtual processor
  --   * Topics allowed on a virtual bus.

  Needed_Memory_Size : Size applies to (process);
  -- Specify the amount of memory needed for a partition
  -- We apply it to process component because we don't
  -- isolate virtual processor, only processes

  Available_Schedulers : type enumeration
  (
      RMS,
      EDF,
      LLF,
      RR,
      TIMESLICE,
      STATIC
  );

  Timeslice : Time applies to (virtual processor);
  -- DEPRECATED at this time

  Major_Frame : Time applies to (processor);

  Scheduler : POK::Available_Schedulers
      applies to (processor, virtual processor);

  Slots: list of Time applies to (processor);
    
  Slots_Allocation:  list of reference (virtual processor) applies to (processor);
  --  List available schedulers
  --  When we use the STATIC scheduler in the virtual processor
  --  The Slots and Slots_Allocation properties are used to determine when
  --  partitions are activated and the timeslice they have for their execution.

  Supported_Error_Code: type enumeration (Deadline_Missed, Application_Error, Numeric_Error, Illegal_Request, Stack_Overflow, Memory_Violation, Hardware_Fault, Power_Fail, Partition_Configuration, Partition_Init, Partition_Scheduling, Partition_Process, Kernel_Init, Kernel_Scheduling);

  Recovery_Errors   : list of POK::Supported_Error_Code applies to (processor, virtual processor, thread);

  Supported_Recovery_Action: type enumeration (Ignore, Confirm, Thread_Restart, Thread_Stop_And_Start_Another, Thread_Stop, Partition_Restart, Partition_Stop, Kernel_Stop, Kernel_Restart, Nothing);

  Recovery_Actions : list of POK::Supported_Recovery_Action applies to (processor, virtual processor, thread);
  -- There is two properties that handle errors and their recovery at the processor and virtual processor level
  -- These two properties must be declared both in the component.
  -- For example, we declare the properties like that:
  --     Recovery_Errors  => (Deadline_Missed, Memory_Violation);
  --     Recovery_Actions => (Ignore, Partition_Restart);
  -- It means that if we have a missed deadline, we ignore the error. But if we get
  -- a memory violation error, we restart the partition.

  Available_BSP : type enumeration
  (
      x86_qemu,
      x86_qemu_vmm,
      prep,
      leon3
  );

  BSP : POK::Available_BSP applies to (processor, system);

  Available_Architectures : type enumeration
  (
      x86, ppc, sparc
  );

  Architecture : POK::Available_Architectures applies to (processor, system);

  -- Deployment properties
  -- Indicate which architecture we use and which bsp

  Source_Location : aadlstring applies to (subprogram);

  -- Indicate where is the object file
  -- that contains this subprogram.


  MILS_Verified : aadlboolean applies to (system, process, device, thread, processor, data);
  --  For verification purpose

  Refresh_Time : Time applies to (data port);

  Hw_Addr : aadlstring applies to (device);

  PCI_Vendor_Id : aadlstring applies to (device);

  PCI_Device_ID : aadlstring applies to (device);

  Device_Name : aadlstring applies to (device);

  Additional_Features : list of POK::Supported_Additional_Features applies to (virtual processor, processor);

  Supported_Additional_Features: type enumeration (libmath, libc_stdlib, libc_stdio, libc_string, io, pci, console, libc, x86_vmm);

  Des_Key : aadlstring applies to (virtual bus);

  Des_Init : aadlstring applies to (virtual bus);

  Blowfish_Key : aadlstring applies to (virtual bus);

  Blowfish_Init : aadlstring applies to (virtual bus);

  Supported_POK_Protocols: type enumeration (ceasar,des,blowfish,unknown);

  Protocol : POK::Supported_POK_Protocols applies to (virtual bus);
end POK;
